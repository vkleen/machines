#!/usr/bin/env zsh

if [[ -n  "$1" ]]; then
    QUTE_FIFO=$1
    shift
fi

_qute_basedir=${XDG_RUNTIME_DIR}/qutebrowser/default

function _do_fzf() {
    FZF_DEFAULT_COMMAND="$HOME/.software/bin/.fzf-url-candidates \"$_qute_basedir\"" \
        fzf --read0 --print0 -e -d '\t' \
        --no-sort \
        --reverse \
        --prompt="open${1+ }$1> " \
        --print-query \
        --expect='alt-enter,enter' \
        --preview-window up:1 \
        --query="$QUTE_URL" \
        --history="$HOME/.fzf-url-history" \
        --preview 'v=$(echo {q} | tr " " "|"); echo {2..} | grep -E "^|$v" -i --color=always' \
    | awk 'BEGIN {FS="\t"; OFS="\t"}; {print $1}'
#        --with-nth '1'\
}

function laststatus() {
    case $? in
        0) return 0 ;;
        1) return 0 ;;
        *) return 1 ;;
    esac
}

function die() {
    tmux detach
    exit $1
}

output=( "${(0)$(_do_fzf "$@")}" )
laststatus || die 1

case "${output[2]}" in
    alt-enter)
        cmd="${output[1]}"
        ;;
    enter)
        cmd="${output[3]}"
        ;;
esac

[[ -z "$cmd" ]] && die 1

if [[ -z "$QUTE_FIFO" ]]; then
    (set -m; trap ':' 1; exec qutebrowser -R "$cmd" >/dev/null 2>&1) &!
else
    printf "open %s %s\n" "$*" "$cmd" >> "$QUTE_FIFO"
fi

die 0
